plugins {
    id "io.spring.dependency-management" version "1.0.6.RELEASE"
    id "com.github.johnrengelman.shadow" version "4.0.2"
    id "application"
    id "java"
    id "net.ltgt.apt-eclipse" version "0.20"
    id "net.ltgt.apt-idea" version "0.20"
    id 'io.franzbecker.gradle-lombok' version '2.0'
    id "com.google.osdetector" version "1.6.1"
    id "com.gorylenko.gradle-git-properties" version "2.0.0"
    id 'com.google.cloud.tools.jib' version '1.0.1'
    id 'jacoco'
    id 'org.hidetake.swagger.generator' version '2.18.1'
    id 'org.unbroken-dome.helm' version '0.4.4'
    id 'org.unbroken-dome.helm-commands' version '0.4.4'
    id 'org.unbroken-dome.helm-publish' version '0.4.4'
    id 'org.unbroken-dome.helm-releases' version '0.4.4'
}

mainClassName = 'com.strapdata.basketapp.Application'

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://jcenter.bintray.com" }
}

dependencyManagement {
    imports {
        mavenBom 'io.micronaut:micronaut-bom:1.1.2'
    }
}

compileJava {
    sourceCompatibility = 8
    targetCompatibility = 8
    options.encoding = 'UTF-8'
    options.compilerArgs = [ '-parameters', '-Xlint:all', '-Xlint:-processing', '-Xlint:-serial' ]
}

compileTestJava {
    options.compilerArgs += '-parameters'
}

configurations {
	// for dependencies that are needed for development only
	developmentOnly 
}

dependencies {
    compile 'org.projectlombok:lombok:1.18.4'

    annotationProcessor "org.projectlombok:lombok:1.18.4"
    annotationProcessor "io.micronaut:micronaut-security"
    annotationProcessor "io.micronaut:micronaut-inject-java"
    annotationProcessor "io.micronaut:micronaut-validation"
    annotationProcessor "io.micronaut.configuration:micronaut-openapi"

    compile "io.micronaut.configuration:micronaut-cassandra"
    compile "io.micronaut:micronaut-http-client"
    compile "io.micronaut:micronaut-http-server-netty"
    compile "io.micronaut:micronaut-inject"
    compile "io.micronaut:micronaut-validation"
    compile "io.micronaut:micronaut-runtime"
    compile "javax.annotation:javax.annotation-api"
    compile "io.micronaut:micronaut-security"
    compile "io.micronaut:micronaut-views"
    runtime "org.apache.velocity:velocity-engine-core:2.0"
    compile "com.fasterxml.jackson.module:jackson-module-parameter-names:2.9.8"
    runtime "ch.qos.logback:logback-classic:1.2.3"
    testAnnotationProcessor "io.micronaut:micronaut-inject-java"

    compile "io.swagger.core.v3:swagger-annotations"

    testCompile "org.junit.jupiter:junit-jupiter-api"
    testCompile "io.micronaut.test:micronaut-test-junit5"
    testRuntime "org.junit.jupiter:junit-jupiter-engine"

    compile group: 'io.vavr', name: 'vavr', version: '0.9.2'
    compile(group: 'com.datastax.cassandra', name:'cassandra-driver-core', version: '3.7.1') { exclude group:  'com.google.guava', module: 'guava' }
    compile(group: 'com.datastax.cassandra', name:'cassandra-driver-mapping', version: '3.7.1') { exclude group:  'com.google.guava', module: 'guava' }
    compile(group: 'com.datastax.cassandra', name:'cassandra-driver-extras', version: '3.7.1') { exclude group:  'com.google.guava', module: 'guava' }
    compile group: 'org.elasticsearch.client', name:'elasticsearch-rest-high-level-client', version: '6.2.3'

    testCompile(group: 'com.strapdata.elassandraunit', name:'elassandra-unit-spring', version: '3.7.1.0') {
        exclude group: 'com.strapdata.elassandraunit', module: 'elassandra-unit'
    }
    testCompile group: 'com.strapdata.elassandraunit', name:'elassandra-unit', version: '3.7.1.0'

    compile group: 'org.codehaus.jackson', name: 'jackson-core-asl', version: '1.9.13'
}

jib {
    from {
        image = 'openjdk:alpine'
    }
    to {
        image = project.image
        tags = [ project.version, 'latest']

        // osxkeychain does not work on linux and CI environments
        // credHelper = 'osxkeychain'
        auth {
            username = registryUsername
            password = registryPassword
        }
    }
    container {
        jvmFlags = ['-Xmx1024m',
                    '-XX:+UnlockExperimentalVMOptions',
                    '-XX:+UseCGroupMemoryLimitForHeap',
                    '-XX:MaxRAMFraction=2',
                    '-XX:+ExitOnOutOfMemoryError'
        ]
        mainClass = 'com.strapdata.basketapp.Application'
        ports = ['8080']
        labels = [ description:'Basketapp REST server']
    }
}

// generate HELM package: gradlew helmPackageBasketappChart
helm {
    charts {
        basketapp {
            chartName = 'basketapp'
            chartVersion = '0.1.0'
            sourceDir = file('src/main/helm/basketapp')
        }
    }
}

shadowJar {
    mergeServiceFiles()
}

jacoco {
    reportsDir = file("$buildDir/customJacocoReportDir")
}

jacocoTestReport {
    reports {
        html.enabled = true
        xml.enabled = true
        csv.enabled = false
    }
}

run.classpath += configurations.developmentOnly
test.classpath += configurations.developmentOnly
run.jvmArgs('-noverify', '-XX:TieredStopAtLevel=1')


// use JUnit 5 platform
test {
    useJUnitPlatform()
}
